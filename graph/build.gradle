plugins {
	id("org.gradle.groovy")
	id("java-gradle-plugin")
	id("org.openjfx.javafxplugin") version '0.0.14'
}

group = "net.twisterrob.gradle"
version = '0.1'

gradlePlugin {
	plugins {
		graph {
			id = "net.twisterrob.graph"
			implementationClass = "net.twisterrob.gradle.graph.GraphPlugin"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation(gradleApi())
	implementation("org.graphstream:gs-core:1.3")
	implementation("com.google.code.gson:gson:2.10.1")
	testImplementation("junit:junit:4.13.2")
}

javafx {
	modules = [ "javafx.controls", "javafx.web", "javafx.swing" ]
}

project.with {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

compileGroovy {
	groovyOptions.configurationScript = file("gradle/compileGroovy.groovy")
}
tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
tasks.withType(GroovyCompile).configureEach {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jar {
	manifest {
		attributes(
				"Implementation-Vendor": project.group,
				"Implementation-Title": project.name,
				"Implementation-Version": project.version,
				"Built-Date": new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssZ").format(new Date())
		)
	}
}
